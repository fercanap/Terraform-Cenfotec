# 17. Funciones <!-- omit in toc -->
> [info](https://developer.hashicorp.com/terraform/language/functions)

# 1. Ejecutar funciones usando la consola
```vim
terraform console

floor(5.9)
title("titulo-pricipal")
max(5, 12, 9)
format("Hello, %s!", "nombre")
replace("1 + 2 + 3", "+", "-")
join("- ", ["var1", "var2", "var3"])
```
# 2. Crear la carpeta /funcion

# 3. Crear el archivo de configuración terraform.tf
```tf
terraform {
  required_providers {
    aws = {
      version = "~> 4.0"
    }
  }
}
```

# 4. Crear el archivo provider.tf
```tf
provider "aws" {
  region = "us-east-1"
}
```

# 5. Crear las variables:
```tf
variable "bucket" {
  type = string
	default = "terraformlabs-test-bucket-100"
}
variable "media" {
  type = set(string)
  default = [
    "../../assets/demo_func01.png",
    "../../assets/demo_func02.jpg",
    "../../assets/demo_func03.png",
  ]
}
```

> Cambiar el nombre del bucket

# 6. Crear en main.tf el recurso aws_s3_bucket
```vim
resource "aws_s3_bucket" "media" {
  bucket = var.bucket
}

resource "aws_s3_object" "upload_media" {
  for_each = var.media

  bucket = aws_s3_bucket.media.id
  source   = each.value
  key      = substr(each.value, 6, 23)
}
```
> Recorre el arreglo "media" y carga las imágenes en S3


# 7. probar los valores de la funcion substr
```vim
terraform console

 substr("../../assets/demo_func01.png", 6, 23)

 [for val in var.media : upper(val)]

```
> Resultado: assets/demo_func01.png

# 8. Output con los nombres de los objetos almacenados en S3
```tf
output "bucket_url" {
  value = {
    for k, v in aws_s3_object.upload_media : k => "${v.bucket}.s3.amazonaws.com/${v.key}"
  }
}
```
> Utiliza la función for k, v para recorrer el arreglo de recursos

# 9. Iniciar y aplicar
```vim
Apply complete! Resources: 4 added, 0 changed, 0 destroyed.

Outputs:

bucket_url = {
  "../../assets/demo_func01.png" = "terraformlabs-test-bucket-100.s3.amazonaws.com/assets/demo_func01.png"
  "../../assets/demo_func02.jpg" = "terraformlabs-test-bucket-100.s3.amazonaws.com/assets/demo_func02.jpg"
  "../../assets/demo_func03.png" = "terraformlabs-test-bucket-100.s3.amazonaws.com/assets/demo_func03.png"
}
```

# 10. Comprobar funcion for k, v
```vim
terraform console

[for k, v in aws_s3_object.upload_media : k]

[for k, v in aws_s3_object.upload_media : v]

[for k, v in aws_s3_object.upload_media : k => "${v.bucket}.s3.amazonaws.com/${v.key}"]
```

> Validar la dirección de las imágenes en la salida de la terminal.
>
> Las imágenes son privadas.

```vim
aws s3api list-objects  --region=us-east-1 --bucket=NOMBRE_DEL_BUCKET
```
# 11. Crear las variables
> [var validation rules](https://terraformguru.com/terraform-certification-using-azure-cloud/27-Input-Variables-Validation-Rules/)

```tf
variable "name" {
  type = string
  description = "Nombre de la instancia"

  validation {
    condition     = length(var.name) > 5
    error_message = "El nombre de la instancias debe ser mayor a 5 caracteres."
  }
}
variable "size" {
  type        = string
  description = "Tamaño de la instancia: nano - micro"

  validation {
    condition     = var.size == "nano" || var.size == "micro"
    error_message = "Las instancias permitidas son: nano || micro."
  }
}
variable "nano" {
  type    = string
  default = "t3a.nano"
}
variable "micro" {
  type    = string
  default = "t3a.micro"
}
```

# 12. Crear una instancia con el nombre y tamaño según la variable INPUT var.size
```tf
resource "aws_instance" "server" {
  ami           = "ami-033b95fb8079dc481"
  instance_type = var.size == "nano" ? var.nano : var.micro
  subnet_id = "subnet-010205a1dea6ff7fc"

  tags = {
    Name = var.name
  }
}
```

# 13. Crear el output que muestre el tipo de instancia creada
```vim
output "instance_size" {
	value = "${aws_instance.server.instance_type}"
}
```

# 14. Aplicar
> Digitar el NOMBRE de la instancia
>
> Digitar el TAMAÑO de la instancia
>
> Apply complete! Resources: 1 added, 0 changed, 0 destroyed.
>
> instance_size = "t3a.micro"

# 15. Destruir los recursos
```tf
terraform destroy -auto-approve
```
> Digitar los valores de las variables name y size

